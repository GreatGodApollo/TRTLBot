@ECHO OFF

SET VERSION_MAJOR_REQUIRED=2
SET VERSION_MINOR_REQUIRED=4
SET VERSION_PATCH_REQUIRED=0

:: Test if ruby is installed
WHERE ruby >NUL 2>NUL
IF %ERRORLEVEL% NEQ 0 GOTO :RUBY_NOT_INSTALLED

:: Get the version (format is A.B.CpDDD)
FOR /F "tokens=2-5 delims=.p " %%A IN ('ruby --version') DO (
	SET VERSION_MAJOR=%%A
	SET VERSION_MINOR=%%B
	SET VERSION_PATCH=%%C
)

:: Test minimum version of ruby
IF %VERSION_MAJOR% LSS %VERSION_MAJOR_REQUIRED% GOTO :WRONG_VERSION
IF %VERSION_MINOR% LSS %VERSION_MINOR_REQUIRED% GOTO :WRONG_VERSION
IF %VERSION_PATCH% LSS %VERSION_PATCH_REQUIRED% GOTO :WRONG_VERSION

@ECHO ON
gem install discordrb sqlite3 sequel httparty
GOTO :EOF

:RUBY_NOT_INSTALLED
ECHO Please install ruby and try again.
GOTO :EOF

:WRONG_VERSION
ECHO Minimum required version of ruby is %VERSION_MAJOR_REQUIRED%.%VERSION_MINOR_REQUIRED%.%VERSION_PATCH_REQUIRED%.
ECHO Please update ruby and try again.
GOTO :EOF
